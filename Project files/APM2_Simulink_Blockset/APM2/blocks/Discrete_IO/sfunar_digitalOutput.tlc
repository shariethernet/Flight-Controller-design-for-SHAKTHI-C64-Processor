%% This code was developed as part of graduate coursework at Embry-Riddle Aeronautical University
%% under the guidance of Dr. Pat Anderson and Dr. Hever Moncayo.

%% Author: Robert F. Hartley
%% Assistant Developers: Francois Hugon, Brian DeRosa, and Christopher Carvalho
%% Support: apm2.simulink@gmail.com

%% Oct. 25, 2012
%% Version 1.0


%% File : sfunar_digitalOutput.tlc

%% Copyright 2010 The MathWorks, Inc.

%implements sfunar_digitalOutput "C"

%include "block_common_includes.tlc"


%% Function: BlockTypeSetup ===============================================
%function BlockTypeSetup(block, system) void

    %% Ensure required header files are included. This is common to all APM2
    %% blocks.
    %if EXISTS("_DONE_SYSTEM_INC_BLOCK_TYPE_SETUP_") == 0
        %assign:: _DONE_SYSTEM_INC_BLOCK_TYPE_SETUP_ = 1
        %if (CompiledModel.TargetStyle!="SimulationTarget")
            %<LibAddToCommonIncludes("Simulink_APM2_Wrapper.h")>
        %endif
    %endif 
    
%endfunction

%% Function: Start ========================================================
%function Start(block, system) Output

%if !SLibCodeGenForSim()
    /* %<Type> (%<ParamSettings.FunctionName>): %<Name> */
    %assign p1_val = LibBlockParameter(p1, "", "", 0)
  
    %% Start up APM2 board. Common to all APM2 blocks.
    %if EXISTS("_DONE_SYSTEM_INIT_") == 0
        %assign :: _DONE_SYSTEM_INIT_ = 1
        /* APM2 System startup function. Required for any blocks. Inserted by:
        %<Type> (%<ParamSettings.FunctionName>): %<Name> */
        Simulink_APM2_Startup();
    %endif

    pinMode(%<p1_val>, OUTPUT);
  
%endif
%endfunction


%% Function: Outputs ======================================================
%function Outputs(block, system) Output

%if !SLibCodeGenForSim()
  %assign p1_val = LibBlockParameter(p1, "", "", 0)
  %assign u1_val = LibBlockInputSignal(0, "", "", 0)

  digitalWrite(%<p1_val>, %<u1_val>);

%endif

%endfunction

%% [EOF]
